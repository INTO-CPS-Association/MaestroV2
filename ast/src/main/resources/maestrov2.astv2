Packages
base org.maestro.ast.node;
analysis org.maestro.ast.analysis;

Tokens
/* This section declared tokens as internal types or external classes
 * internal definitions:
 *  plus='+';
 *
 * external
 *  location = 'java:org.overture.ast.lex.LexLocation';
 */

    java_String = 'java:java.lang.String';
    java_Boolean = 'java:java.lang.Boolean';
    java_Integer = 'java:java.lang.Integer';
    java_Double = 'java:java.lang.Double';
    java_Long = 'java:java.lang.Long';

Abstract Syntax Tree
/* This section declares the AST by giving root nodes and their possible sub classes
 *
 * exp {-> package='org.overture.ast.expressions'}
 *      = {subclass1} [field_name]:field_type_must_either_be_root_or_token ...
 *      ;
 *
 */

specification {-> package='org.maestro.ast.specification'}
    =   {language} [name]:identifier [functions]:type.function*
    |   {simulation} [body]:stm*
    ;

exp {-> package='org.maestro.ast.expression'}
    =   {variable} [name]:identifier
    |   {realLiteral} [value]:java_Double
    |   {intLiteral} [value]:java_Integer
    |   {stringLiteral} [value]:java_String
    |   {identifier} [value]:java_String
    |   {booleanLiteral} [value]:java_Boolean
    |   {load} [uri]:exp.stringLiteral [importName]:identifier
    |   {apply} [root]:exp  [functionName]:identifier [args]:exp*
    |   {seqComp} [members]:exp*
    ;

stm {-> package='org.maestro.ast.statements'}
    =   {unload} [fmu]:identifier
    |   {assignment} [target]:identifier [exp]:exp
    |   {variableDeclaration} [type]:type [name]:identifier [initializer]:exp
    |   {import} [name]:identifier
    |   {while} [exp]:exp [statement]:block
    |   {if} [exp]:exp [thenStm]:block [elseStm]:block
    |   {block} [body]:stm*
    ;

type {-> package='org.maestro.ast.types'}
    =   #basic
    |   {name} [name]:identifier
    |   {class} [name]:identifier [functions]:type.function*
    |   {function} [name]:identifier (parameters):type* (result):type
    |   {array} [size]:java_Integer [type]:type.#basic
    ;

#basic {-> package='org.maestro.ast.types'}
    =   {boolean}
    |   {real}
    |   {string}
    |   {int}
    ;
